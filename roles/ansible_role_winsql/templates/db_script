--- example SQL script
--- Creates a database 'myDB' with a 'myAG' avail group
--- Database files are in the data / logs directory as labeled by the OS


--- First, pick up the disk volume names for data and logs

-- Enable Ole Automation
EXECUTE sp_configure 'show advanced options', 1
RECONFIGURE WITH OVERRIDE
GO
EXECUTE sp_configure 'Ole Automation Procedures', 1
RECONFIGURE WITH OVERRIDE
GO

-- Create function for filesystem volumes
-- Credit: Percy Reyes

USE [master]
GO
CREATE FUNCTION [dbo].[ufn_LogicalDiskDrives]()
RETURNS @DriveList Table
  (
   [DriveLetter]  CHAR(1)
  ,[VolumeName]  VARCHAR(255)
  ,[FileSystem]  VARCHAR(50)
  )
AS

BEGIN
  DECLARE @DriveLetter_ASCII_Code  INT
  DECLARE @FileSystemInstance  INT
  DECLARE @DriveCount  INT
  DECLARE @DriveCollection  INT
  DECLARE @Drive  INT
  DECLARE @Property  NVARCHAR(100)    
  DECLARE @DriveLetter  VARCHAR(1)  
  DECLARE @FileSystem VARCHAR(128)
  DECLARE @VolumeName VARCHAR(128)
  DECLARE @IsReady VARCHAR(5)
  
  --Creating a File System Object for getting files or disk info.
  exec sp_OACreate 'Scripting.FileSystemObject', @FileSystemInstance OUT

  --Getting the collection of drives
  exec sp_OAGetProperty @FileSystemInstance,'Drives', @DriveCollection OUT

  --Getting the count of drives from collection
  exec sp_OAGetProperty @DriveCollection,'Count', @DriveCount OUT

  --starting from Drive "A" (ASCII 65) 
  SET @DriveLetter_ASCII_Code = 65
  --to "Z" (ASCII 90)
  WHILE @DriveLetter_ASCII_Code <= 90

  BEGIN

  -- Creating the instance drive from Drive Collection 
  SET @Property = 'item("'+CHAR(@DriveLetter_ASCII_Code)+'")'
  exec sp_OAGetProperty @DriveCollection,@Property, @Drive OUT

  -- Getting the drive letter property
  exec sp_OAGetProperty @Drive,'DriveLetter', @DriveLetter OUT

  IF @DriveLetter = CHAR(@DriveLetter_ASCII_Code)
    BEGIN   
    -- Getting more properties from each drive   
    exec sp_OAGetProperty @Drive,'VolumeName', @VolumeName OUT 
    exec sp_OAGetProperty @Drive,'FileSystem', @FileSystem OUT
    exec sp_OAGetProperty @Drive,'IsReady'  , @IsReady OUT; 
            
   IF @IsReady='True'
     INSERT INTO @DriveList ( [DriveLetter],[FileSystem],[VolumeName] )
     VALUES( @DriveLetter,@FileSystem,@VolumeName)
                             
   END
        -- forward next drive
        SET @DriveLetter_ASCII_Code = @DriveLetter_ASCII_Code +1
  END 
  
  EXEC sp_OADestroy @Drive 
  EXEC sp_OADestroy @DriveCollection

  RETURN
END
GO


-- set drive vars from function

DECLARE @log_drive NVARCHAR(50)
DECLARE @log_path NVARCHAR(50)
DECLARE @data_drive NVARCHAR(50)
DECLARE @data_path NVARCHAR(50)
DECLARE @dbName NVARCHAR(50);

-- set the databse name to "MyDB"
SET @dbName = N'MyDB';

select @log_drive = [DriveLetter]
from [dbo].[ufn_LogicalDiskDrives]()
where Volumename = 'logs'
set @log_path = @log_drive+":\" + @dbName + "_log.ldf"
print @log_path

select @data_drive = [DriveLetter]
from [dbo].[ufn_LogicalDiskDrives]()
where Volumename = 'data'
set @data_path =  @data_drive+":\" + @dbName + "_data.mdf"
print @data_path


-- Create the databse using the dbName defined above

-- Construct the SQL command
DECLARE @sql NVARCHAR(MAX);
SET @sql = N'
CREATE DATABASE ' + QUOTENAME(@dbName) + '
ON PRIMARY 
(
    NAME = ' + QUOTENAME(@dbName + '_Data') + ',
    FILENAME = ''' + @data_path + '''
)
LOG ON
(
    NAME = ' + QUOTENAME(@dbName + '_Log') + ',
    FILENAME = ''' + @log_path + '''
)';

-- Print the SQL command for debugging purposes
PRINT @sql;

-- Execute the SQL command
EXEC sp_executesql @sql;


-- Backup the DB created above 

DECLARE @backup_data_path NVARCHAR(50)
DECLARE @backup_log_path NVARCHAR(50)

set @backup_data_path =  @data_drive+":\" + @dbName +".bak"
set @backup_log_path =  @log_drive+":\" + @dbName +".log"

BACKUP DATABASE @dbName
TO DISK = @backup_data_path ;

BACKUP LOG @dbName 
TO DISK = @backup_log_path ;

GO


-- Now that we have a DB which is backed-up, an AG can be created


--- write connect argument to secondary to file
DECLARE @secondary varchar(128)
set @secondary = (select member_name from master.sys.dm_hadr_cluster_members where member_name like 'sql-%' and member_name not like @@servername);
:Out $(TEMP)\secondary.temp
PRINT ':connect ' +  @secondary + '';
GO
:Out stdout

--- write connect argument to primary to file
:Out $(TEMP)\primary.temp
PRINT ':connect ' +  @@SERVERNAME + '';
GO
:Out stdout


:r $(TEMP)\primary.temp

USE [master]

GO

CREATE ENDPOINT [Hadr_endpoint] 
	AS TCP (LISTENER_PORT = 5022)
	FOR DATA_MIRRORING (ROLE = ALL, ENCRYPTION = REQUIRED ALGORITHM AES)

GO

IF (SELECT state FROM sys.endpoints WHERE name = N'Hadr_endpoint') <> 0
BEGIN
	ALTER ENDPOINT [Hadr_endpoint] STATE = STARTED
END

GO

:r $(TEMP)\primary.temp

IF EXISTS(SELECT * FROM sys.server_event_sessions WHERE name='AlwaysOn_health')
BEGIN
  ALTER EVENT SESSION [AlwaysOn_health] ON SERVER WITH (STARTUP_STATE=ON);
END
IF NOT EXISTS(SELECT * FROM sys.dm_xe_sessions WHERE name='AlwaysOn_health')
BEGIN
  ALTER EVENT SESSION [AlwaysOn_health] ON SERVER STATE=START;
END

GO

:r $(TEMP)\secondary.temp 

USE [master]

GO

CREATE ENDPOINT [Hadr_endpoint] 
	AS TCP (LISTENER_PORT = 5022)
	FOR DATA_MIRRORING (ROLE = ALL, ENCRYPTION = REQUIRED ALGORITHM AES)

GO

IF (SELECT state FROM sys.endpoints WHERE name = N'Hadr_endpoint') <> 0
BEGIN
	ALTER ENDPOINT [Hadr_endpoint] STATE = STARTED
END


GO

:r $(TEMP)\secondary.temp 

IF EXISTS(SELECT * FROM sys.server_event_sessions WHERE name='AlwaysOn_health')
BEGIN
  ALTER EVENT SESSION [AlwaysOn_health] ON SERVER WITH (STARTUP_STATE=ON);
END
IF NOT EXISTS(SELECT * FROM sys.dm_xe_sessions WHERE name='AlwaysOn_health')
BEGIN
  ALTER EVENT SESSION [AlwaysOn_health] ON SERVER STATE=START;
END

GO

:r $(TEMP)\primary.temp

USE [master]

GO

--- now create the AG 'MyAG'

-- Define your T-SQL variables
DECLARE @PrimaryServer NVARCHAR(128) = @@SERVERNAME;
DECLARE @SecondaryServer NVARCHAR(128) = (select member_name from master.sys.dm_hadr_cluster_members where member_name like 'sql-%' and member_name not like @@servername);
DECLARE @AvailabilityGroupName NVARCHAR(128) = 'MyAG';
DECLARE @DatabaseName NVARCHAR(128) = 'MyDB';

print @primaryServer
print @secondaryServer

-- Construct the dynamic SQL command
DECLARE @sql NVARCHAR(MAX);

SET @sql = N'
CREATE AVAILABILITY GROUP ' + QUOTENAME(@AvailabilityGroupName) + N'
WITH (AUTOMATED_BACKUP_PREFERENCE = SECONDARY,
DB_FAILOVER = OFF,
DTC_SUPPORT = NONE,
REQUIRED_SYNCHRONIZED_SECONDARIES_TO_COMMIT = 0)
FOR DATABASE ' + QUOTENAME(@DatabaseName) + N'
REPLICA ON
    N''' + @PrimaryServer + N''' WITH (
        ENDPOINT_URL = N''TCP://' + @PrimaryServer + N':5022'',
        FAILOVER_MODE = AUTOMATIC,
        AVAILABILITY_MODE = SYNCHRONOUS_COMMIT,
        BACKUP_PRIORITY = 50, 
        SEEDING_MODE = AUTOMATIC, 
        SECONDARY_ROLE (ALLOW_CONNECTIONS = NO)),
    N''' + @SecondaryServer + N''' WITH (
        ENDPOINT_URL = N''TCP://' + @SecondaryServer + N':5022'',
        FAILOVER_MODE = AUTOMATIC,
        AVAILABILITY_MODE = SYNCHRONOUS_COMMIT,
        BACKUP_PRIORITY = 50, 
        SEEDING_MODE = AUTOMATIC, 
        SECONDARY_ROLE (ALLOW_CONNECTIONS = NO));';

-- Print the constructed SQL for debugging purposes
PRINT @sql;


-- Execute the dynamic SQL command
EXEC sp_executesql @sql;


--- Finally, connect to the secondary to join to the AG


GO

:r $(TEMP)\secondary.temp 

ALTER AVAILABILITY GROUP [MyAG] JOIN;

GO

ALTER AVAILABILITY GROUP [MyAG] GRANT CREATE ANY DATABASE;

GO


GO